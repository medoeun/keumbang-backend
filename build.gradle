plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'org.keumbang'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group = 'org.keumbang'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // SpringBoot
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-batch'

        // H2
        runtimeOnly 'com.h2database:h2'

        // Redis
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        // Security
        implementation 'org.springframework.boot:spring-boot-starter-security'

        // Swagger
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

        // JWT
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        // MariaDB
        runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Argon2
        implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy 'jacocoTestReport'
    }

    jacoco {
        toolVersion = '0.8.11'
    }

    jacocoTestReport {
        reports {
            html.required = true
            xml.required = true
            csv.required = false
        }
    }
}

project(':auth-server') {
    dependencies {
        implementation project(':common')  // common 모듈 참조
    }
}

project(':resource-server') {
    dependencies {
        implementation project(':common')  // common 모듈 참조
    }
}

project(':common') {
    // common 모듈은 실행 가능한 애플리케이션이 아니므로 bootJar 비활성화
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
    // common 모듈에서 Jacoco 리포트를 생성하지 않도록 설정
    tasks.withType(Test) {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        enabled = false  // Jacoco 리포트 생성을 비활성화
    }
}